; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define protected
#ifndef SetupName
  #define SetupName "CopyCat"
; #define README_FILE "..\License\Readme.txt"
; #define LICENSE_FILE "..\License\License.txt"
#endif
#define VERSION "3.9.4"
#define MAXCOMPVERSIONS 19

[Setup]
AppName=CopyCat
#ifdef Trial
AppVerName=CopyCat evaluation v.{#VERSION}
#define README_FILE "..\License\Readme.txt"
#define LICENSE_FILE "..\License\License.txt"
#else
AppVerName=CopyCat v.{#VERSION}
#define README_FILE "..\License\Readme_reg.txt"
#define LICENSE_FILE "..\License\License_reg.txt"
#endif
AppPublisherURL=http://www.copycat.fr
AppSupportURL=http://www.copycat.fr
AppUpdatesURL=http://www.copycat.fr
DefaultDirName={pf}\CopyCat\
DisableDirPage=false
DefaultGroupName=CopyCat
VersionInfoVersion=
OutputBaseFilename={#SetupName}_{#VERSION}
InternalCompressLevel=max
ShowLanguageDialog=yes
WizardImageBackColor=clSilver
WizardSmallImageFile=Microtec2.bmp
WindowVisible=false
BackColor=clNavy
BackColor2=$ee928a
WizardImageStretch=true
AppCopyright=Microtec Communications
WizardImageFile=Wizard.bmp
BackColorDirection=toptobottom
OutputDir=.
SolidCompression=true
#ifdef UserPassword
Password={#UserPassword}
Encryption=false
#endif
AppVersion={#VERSION}
AllowNoIcons=true
AlwaysShowComponentsList=true
InfoBeforeFile={#README_FILE}
InfoAfterFile=
LicenseFile={#LICENSE_FILE}
VersionInfoCopyright=Microtec Informatique
UserInfoPage=true
UninstallDisplayName=Microtec CopyCat v.{#VERSION}

[Tasks]

[Files]
#define i 0

#sub CheckVersions
#undef public BCB
#undef public Delphi
#undef public BDS
#undef public CRS
#if i == 0
  #undef public Delphi
  #define public DIR "FPC"
  #define public RegDir ""
  #define public suffixe "";
  #define public DESC "FreePascal";
#elif i == 1
  #define public Delphi
  #define public DIR "D5"
  #define public RegDir "Software\Borland\Delphi\5.0"
  #define public suffixe "D5";
  #define public DESC "Delphi 5";
#elif i == 2
  #define public Delphi
  #define public DIR "D6"
  #define public RegDir "Software\Borland\Delphi\6.0"
  #define public suffixe "D6";
  #define public DESC "Delphi 6";
#elif i == 3
  #define public Delphi
  #define public DIR "D7"
  #define public RegDir "Software\Borland\Delphi\7.0"
  #define public suffixe "D7";
  #define public DESC "Delphi 7";
#elif i == 4
  #define public BDS
  #define public DIR "D2005"
  #define public RegDir "Software\Borland\BDS\3.0"
  #define public suffixe "2005";
  #define public DESC "Delphi 2005";
#elif i == 5
  #define public BDS
  #define public DIR "D2006"
  #define public RegDir "Software\Borland\BDS\4.0"
  #define public suffixe "2006";
  #define public DESC "Delphi/C++Builder 2006 for Win32";
#elif i == 6
  #define public BDS
  #define public DIR "D2007"
  #define public RegDir "Software\Borland\BDS\5.0"
  #define public suffixe "2007";
  #define public DESC "Delphi/C++Builder 2007 for Win32";
#elif i == 7
  #define public BDS
  #define public DIR "D2009"
  #define public RegDir "Software\CodeGear\BDS\6.0"
  #define public suffixe "2009";
  #define public DESC "Delphi/C++Builder 2009 for Win32";
#elif i == 8
  #define public BDS
  #define public DIR "D2010"
  #define public RegDir "Software\CodeGear\BDS\7.0"
  #define public suffixe "2010";
  #define public DESC "Delphi/C++Builder 2010 for Win32";
#elif i == 9
  #define public BDS
  #define public DIR "D2011"
  #define public RegDir "Software\Embarcadero\BDS\8.0"
  #define public suffixe "2011";
  #define public DESC "Delphi/C++Builder XE (2011) for Win32";
#elif i == 10
  #define public BDS
  #define public DIR "D2012"
  #define public RegDir "Software\Embarcadero\BDS\9.0"
  #define public suffixe "2012";
  #define public DESC "Delphi/C++Builder XE2 (2012) for Win32";
#elif i == 11
  #define public BDS
  #define public DIR "D2013"
  #define public RegDir "Software\Embarcadero\BDS\10.0"
  #define public suffixe "2013";
  #define public DESC "Delphi/C++Builder XE3 (2013) for Win32";
#elif i == 12
  #define public BDS
  #define public DIR "D2014"
  #define public RegDir "Software\Embarcadero\BDS\11.0"
  #define public suffixe "2014";
  #define public DESC "Delphi/C++Builder XE4 (2014) for Win32";
#elif i == 13
  #define public BDS
  #define public DIR "D2015"
  #define public RegDir "Software\Embarcadero\BDS\12.0"
  #define public suffixe "XE5";
  #define public DESC "Delphi/C++Builder XE5 (2015) for Win32 / iOS / Android";
#elif i == 14
  #define public BDS
  #define public DIR "XE6"
  #define public RegDir "Software\Embarcadero\BDS\14.0"
  #define public suffixe "XE6";
  #define public DESC "Delphi/C++Builder XE6 for Win32 / iOS / Android";
#elif i == 15
  #define public BDS
  #define public DIR "XE7"
  #define public RegDir "Software\Embarcadero\BDS\15.0"
  #define public suffixe "XE7";
  #define public DESC "Delphi/C++Builder XE7 for Win32 / iOS / Android";
#elif i == 16
  #define public BDS
  #define public DIR "XE8"
  #define public RegDir "Software\Embarcadero\BDS\16.0"
  #define public suffixe "XE8";
  #define public DESC "Delphi/C++Builder XE8 for Win32 / iOS / Android";
#elif i == 17
  #define public BDS
  #define public DIR "D10"
  #define public RegDir "Software\Embarcadero\BDS\17.0"
  #define public suffixe "D10";
  #define public DESC "Delphi 10 Seattle for Windows / MacOS / iOS / Android";
#elif i == 18
  #define public BDS
  #define public DIR "D101"
  #define public RegDir "Software\Embarcadero\BDS\18.0"
  #define public suffixe "D10_1";
  #define public DESC "Delphi 10.1 Berlin for Windows / MacOS / iOS / Android";
#elif i == 19
  #define public BDS
  #define public DIR "D102"
  #define public RegDir "Software\Embarcadero\BDS\19.0"
  #define public suffixe "D10_2";
  #define public DESC "Delphi 10.2 Tokyo for Windows / MacOS / iOS / Android / Linux";
#endif

#ifdef Trial
  #define public SourceDir "Trial\" + DIR
#else
  #define public SourceDir DIR
#endif
#endsub

#sub AddSource
#expr CheckVersions

#ifdef FullSource
Source: ..\Cc*; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.bak, *.dtx, *.~*, *.dcu, *.hpp, *.obj, CcTrial.*, *.o; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
; always need Source: ..\CopyCat_{#suffixe}.*; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif

Source: ..\CopyCat.dcr; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\CC.inc; DestDir: {app}\{#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly; Components: {#DIR}

#if i == 0
Source: ..\CopyCat.lpk; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#else
Source: ..\CopyCat_{#suffixe}.*; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\{#SourceDir}\CopyCat_{#suffixe}.bpl; DestDir: {sys}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\{#SourceDir}\CopyCat_{#suffixe}.bpl; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\{#SourceDir}\CopyCat_{#suffixe}.bpi; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
Source: ..\{#SourceDir}\CopyCat_{#suffixe}.lib; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
Source: ..\{#SourceDir}\CopyCat_{#suffixe}.dcp; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
Source: ..\ADO\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\ADO\CopyCatADO_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
Source: ..\ADO\CopyCatADO_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
#if i < 15
Source: ..\FIBPlus\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\FIBPlus\CopyCatFIB_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\FIBPlus\CopyCatFIB_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif

Source: ..\IBX\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBX\CopyCatIBX_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBX\CopyCatIBX_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBX\CopyCatIBX_BCB.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly

#if i < 18
Source: ..\IBO\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBO\CopyCatIBO_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBO\CopyCatIBO_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif

Source: ..\XMLRPC\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\XMLRPC\*.inc; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\XMLRPC\CopyCatXMLRPC_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\XMLRPC\CopyCatXMLRPC_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\RTC\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\RTC\CopyCatRTC_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\RTC\CopyCatRTC_C6.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\NexusDB\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\NexusDB\CopyCatNexusDB_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#if i >= 12
Source: ..\FireDAC\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\FireDAC\CopyCatFireDAC_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif
#if i >= 13
Source: ..\DataSnap\*.pas; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\DataSnap\CopyCatDataSnap_{#suffixe}.*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: *.~*, *.tds, *.bpl, *.bpi, *.lib, *.obj, *.map, *.bpr; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif
Source: ..\{#SourceDir}\Cc*; DestDir: {app}\{#DIR}; Components: {#DIR}; Excludes: CcTrial.*, *.txvpck; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#endif

#ifdef Trial
; do need CcTrial.dfm to compile DPK into BPL and it must be binary (not text) files
Source: ..\CcTrial.dfm; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
#if i > 0
Source: ..\{#SourceDir}\CcTrial.dcu; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\{#SourceDir}\CcTrial.hpp; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
Source: ..\{#SourceDir}\CcTrial.obj; DestDir: {app}\{#DIR}; Components: {#DIR}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist
#endif
#endif

;Source: ..\{#DIR}\Connectors\*; DestDir: {app}\{#DIR}\Connectors; Components: {#DIR}; Excludes: CcTrial.*, *.txvpck; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly skipifsourcedoesntexist

#endsub

#for {i=0;i<=MAXCOMPVERSIONS;i++} AddSource

Source: ..\Examples\*; DestDir: {app}\Examples; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj, *.h, *.dcu, *.il?, *.*pch, *.zip, *.identcache, *.#*, .svn; Components: Examples; Languages: ; Flags: recursesubdirs sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\changelog.txt; DestDir: {app}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Getting Started.txt; DestDir: {app}; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly

Source: {#LICENSE_FILE}; DestDir: {app}; DestName: License.txt; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: {#README_FILE}; DestDir: {app}; DestName: Readme.txt; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly

; The "Other Connectors" directory contains various secondary connectors which we don't want to support
Source: ..\UIB\*; DestDir: {app}\Other Connectors; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj, *.h, *.dcu, .svn; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Zeos\*; DestDir: {app}\Other Connectors; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj, *.h, *.dcu, .svn; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\UniDAC\*; DestDir: {app}\Other Connectors; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj, *.h, *.dcu, .svn; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\IBDAC\*; DestDir: {app}\Other Connectors; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj, *.h, *.dcu, .svn; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly

Source: ..\..\CopyCatJava\*; DestDir: {app}\JavaServer; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatJava\copycat\*; DestDir: {app}\JavaServer\copycat\; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatJava\copycat\xmlrpc\*; DestDir: {app}\JavaServer\copycat\xmlrpc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatJava\test\*; DestDir: {app}\JavaServer\test; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatPHP\*; DestDir: {app}\PHPServer; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatPHP\.settings\*; DestDir: {app}\PHPServer\copycat\; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\..\CopyCatPHP\xmlrpc\*; DestDir: {app}\PHPServer\copycat\xmlrpc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Doc\CopyCat.als; DestDir: {app}\Doc; Components: Doc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Doc\CopyCat.cnt; DestDir: {app}\Doc; Components: Doc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Doc\CopyCat.hlp; DestDir: {app}\Doc; Components: Doc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
Source: ..\Doc\chm\CopyCat.chm; DestDir: {app}\Doc; Components: Doc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
;Source: ..\Doc\GettingStarted.chm; DestDir: {app}\Doc; Components: Doc; Flags: sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
;Source: ..\Test\*; DestDir: {app}\Test; Excludes: *.exe,*.tds,*.map, *.~*, *.elf, *.obj; Components: Examples; Languages: ; Flags: recursesubdirs sortfilesbyextension overwritereadonly ignoreversion uninsremovereadonly
[Dirs]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; The line below just added a blank copycat folder in the PF\CopyCat folder:
; Name: {app}\copycat
[Icons]
Name: {group}\{cm:UninstallProgram, Microtec CopyCat}; Filename: {uninstallexe}

[Run]
Filename: {app}\Getting Started.txt; Description: Show getting started information; Flags: skipifdoesntexist postinstall runmaximized shellexec; Components: ; Languages: 

[LangOptions]
CopyrightFontName=Arial
CopyrightFontSize=10

[Languages]
Name: Default; MessagesFile: compiler:Default.isl
[_ISTool]
UseAbsolutePaths=false
[Registry]

#sub AddReg
#expr CheckVersions
Root: HKCU; Subkey: {#RegDir}\Known packages; ValueType: string; ValueName: {app}\{#DIR}\CopyCat_{#Suffixe}.bpl; ValueData: CopyCat v.{#VERSION}; Components: {#DIR}
Root: HKCU; Subkey: {#RegDir}\Library; ValueType: string; ValueName: Search Path; ValueData: "{code:GetSearchPath|{#RegDir}};{app}\{#DIR}\"; Components: {#DIR}; Check: CheckSearchPath('{#RegDir}', ExpandConstant('{app}\{#DIR}'))
Root: HKCU; Subkey: {#RegDir}\Library; ValueType: string; ValueName: Search Path; ValueData: "{code:GetSearchPath|{#RegDir}};{app}\{#DIR}\Connectors"; Components: {#DIR}; Check: CheckSearchPath('{#RegDir}', ExpandConstant('{app}\{#DIR}\Connectors'))
#endsub
#for {i=1;i<=MAXCOMPVERSIONS;i++} AddReg

[Components]
#sub InstallerComponents
#expr CheckVersions
Name: {#DIR}; Description: {#DESC}; Types: compact full custom; Check: {#DIR}Installed
#endsub
#for {i=0;i<=MAXCOMPVERSIONS;i++} InstallerComponents

Name: Doc; Description: Documentation; Types: full
Name: Examples; Description: Example projects; Types: full

[Code]
function GetSearchPath(RegDir:String):String;
begin
  RegQueryStringValue (HKCU, RegDir + '\Library', 'Search Path', Result);
end;

function GetRootDir(RegDir:String):String;
begin
  RegQueryStringValue (HKCU, RegDir, 'RootDir', Result);
  if Result = '' then
    RegQueryStringValue (HKLM, RegDir, 'RootDir', Result);
end;

function GetProjectDir(RegDir, DefaultDir:String):String;
begin
  RegQueryStringValue(HKCU, RegDir, 'RootDir', Result);
  Result := Result + '\Projects';
  if not FileExists(Result) then
    Result := DefaultDir;
end;

function CheckSearchPath(RegDir, Dir:String):Boolean;
begin
  Result := (Pos(Dir, GetSearchPath(RegDir)) = 0);
end;

#sub CheckInstalledIDEs
#expr CheckVersions
function {#DIR}Installed:Boolean;
begin
#if i == 0
  Result := RegKeyExists(HKCU, '{#RegDir}');
#else
  Result := True;
#endif
end;
#endsub
#for {i=0;i<=MAXCOMPVERSIONS;i++} CheckInstalledIDEs

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
#sub DelReg
#expr CheckVersions
  RegDeleteValue(HKCU, '{#RegDir}\Known packages', ExpandConstant('{app}\{#DIR}\CopyCat_{#Suffixe}.bpl'));
#endsub
#for {i=1;i<=MAXCOMPVERSIONS;i++} DelReg
  end;
end;
